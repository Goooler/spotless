apply plugin: 'dev.equo.ide'
equoIde {
	branding().title('Spotless').icon(file('_images/spotless_logo.png'))
	welcome().openUrl('https://github.com/diffplug/spotless/blob/main/CONTRIBUTING.md')
	gradleBuildship().autoImport('.')
}

repositories {
	mavenCentral()
}

apply from: rootProject.file('gradle/java-publish.gradle')
apply from: rootProject.file('gradle/changelog.gradle')
allprojects {
	apply from: rootProject.file('gradle/spotless.gradle')
}
apply from: rootProject.file('gradle/spotless-freshmark.gradle')

spotless {
	groovyGradle {
		target '*.gradle', 'gradle/*.gradle'
	}
	format 'dotfiles', {
		target '.gitignore', '.gitattributes', '.editorconfig'
		indentWithSpaces(2)
		trimTrailingWhitespace()
		endWithNewline()
	}
}

configurations.create("spotlessDeps") {
	canBeConsumed = false
	transitive = true
	visible = false
	attributes { attr ->
		attr.attribute(Bundling.BUNDLING_ATTRIBUTE, project.getObjects().named(Bundling.class, Bundling.EXTERNAL))
	}
}

dependencies {
	spotlessDeps "com.pinterest.ktlint:ktlint-cli:1.0.0"
}

tasks.register("spotlessDeps") {
	configurations.spotlessDeps.resolve().forEach {
		println "Resolved: ${it.name}"
	}
}
